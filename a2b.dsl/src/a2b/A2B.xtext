grammar a2b.A2B with org.eclipse.xtext.common.Terminals

generate a2B "http://www.A2B.a2b"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	element+=Instruction*
;

Instruction:
	DB | DW | DD | Base64 | ORG | INCLUDE | MAC | IP | HXS |  {BE} BE | {LE} LE | {PCAP} PCAP
;

DB:
	'DB' (stringValue=BYTE | intValue=INT)
;

DW:
	'DW' (stringValue=WORD | intValue=INT)
;

DD:
	'DD' (stringValue=DOUBLEWORD | longValue=LONG)
;

Base64:
	'B64' value=STRING
;	

ORG:
	'ORG' value=INT
;

INCLUDE:
	'INCLUDE' value=PATH
;

MAC:
	'MAC' value=MACADDRESS
;

IP:
	'IP' value=IPADDRESS
;

HXS:
	'HXS' value=HEXSTRING
;

BE:
	'BE'
;

LE:
	'LE'
;

PCAP:
	'PCAP'
;

LONG returns ecore::ELong:
	INT
;

terminal DECIMALBYTE_:
	(('1'..'9')?('0'..'9') | ('1')('0'..'9')('0'..'9') | ('2')('0'..'4')('0'..'9') | ('2')('5')('0'..'5'))
;

terminal BINARY_:
	(('0' | '1')('0' | '1')('0' | '1')('0' | '1')('0' | '1')('0' | '1')('0' | '1')('0' | '1'))
;

terminal HEX_:
	(('A'..'F' | 'a'..'f' | '0'..'9')('A'..'F' | 'a'..'f' | '0'..'9'))
;

terminal BYTE:
	'0b'BINARY_ |
	'0x'HEX_
;

terminal WORD:
	'0b'BINARY_ BINARY_ | 
	'0x'HEX_ HEX_
;

terminal DOUBLEWORD:
	'0b'BINARY_ BINARY_ BINARY_ BINARY_ | 
	'0x'HEX_ HEX_ HEX_ HEX_
;

terminal PATH:
	(('A'..'Z')(':'))?(('/')(('a'..'z'|'A'..'Z')*))+(('.')('a'..'z'|'A'..'Z')*)+
;

terminal MACADDRESS:
	HEX_':'HEX_':'HEX_':'HEX_':'HEX_':'HEX_
;

terminal IPADDRESS:
	DECIMALBYTE_'.'DECIMALBYTE_'.'DECIMALBYTE_'.'DECIMALBYTE_
;

terminal HEXSTRING:
	'0h'(HEX_)+
;